plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id "com.jfrog.bintray" version "1.8.4"
}

apply plugin: 'maven-publish'
apply plugin: 'jacoco'

group 'eu.afse'
version '0.9.6'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'io.github.microutils:kotlin-logging:1.7.8'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

bintray {
    user = 'user'
    key = 'key'
    publications = ['mavenJava']
    
    pkg {
        repo = 'json-logic-kotlin'
        name = 'eu.afse.jsonlogic'
        userOrg = 'advantagefse'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/advantagefse/json-logic-kotlin'

        version {
            name = project.version
            released = new Date()
            vcsTag = "v${project.version}"
        }
    }
}

task sourcesJar(type: Jar, dependsOn: project.classes) {
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: project.javadoc) {
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId project.bintray.pkg.name
            from components.java

            artifact sourcesJar {
                classifier = 'sources'
            }
            artifact javadocJar {
                classifier = 'javadoc'
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

test {
    useJUnitPlatform()
}
